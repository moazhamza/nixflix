{
  system ? builtins.currentSystem,
  pkgs ? import <nixpkgs> { inherit system; },
  nixosModules,
}:
let
  inherit (pkgs) lib;
  jellyfinPlugins = import ../../lib/jellyfin-plugins.nix { inherit lib; };
  secrets = import ../../lib/secrets { inherit lib; };
  manifestHash =
    file:
    builtins.convertHash {
      hash = builtins.hashFile "sha256" file;
      hashAlgo = "sha256";
      toHashFormat = "sri";
    };

  # Helper to evaluate a NixOS configuration without building
  evalConfig =
    modules:
    import "${pkgs.path}/nixos/lib/eval-config.nix" {
      inherit system;
      modules = [
        nixosModules
        {
          # Minimal NixOS config stubs needed for evaluation
          nixpkgs.hostPlatform = system;
        }
      ]
      ++ modules;
    };

  # Test helper to assert conditions
  assertTest =
    name: cond:
    pkgs.runCommand "unit-test-${name}" { } ''
      ${lib.optionalString (!cond) "echo 'FAIL: ${name}' && exit 1"}
      echo 'PASS: ${name}' > $out
    '';

  check = name: cond: ''
    ${lib.optionalString (!cond) "echo 'FAIL: ${name}' && exit 1"}
    echo 'PASS: ${name}'
  '';
in
{
  profilarr-service-generation =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;
            profilarr = {
              enable = true;
              apiKey = "0123456789abcdef0123456789abcdef";
              connectors = [
                {
                  name = "Radarr";
                  type = "radarr";
                  url = "http://127.0.0.1:7878";
                  apiKey = "radarr-secret";
                  sync = {
                    mediaManagement = {
                      database = "Dictionarry";
                      naming = "Radarr";
                      qualityDefinitions = "Radarr";
                      mediaSettings = "Radarr";
                    };
                    delayProfile = {
                      database = "Dictionarry";
                      profile = "Radarr";
                    };
                    qualityProfiles = [
                      {
                        database = "Dictionarry";
                        profile = "2160p Remux";
                      }
                    ];
                  };
                }
              ];
            };
          };
        }
      ];
      generated = config.config;
      container = generated.virtualisation.oci-containers.containers.profilarr;
      databasesService = generated.systemd.services.profilarr-databases;
      connectorsService = generated.systemd.services.profilarr-connectors;
      hasExpectedDatabases =
        generated.nixflix.profilarr.databases == [
          {
            name = "Dictionarry";
            repositoryUrl = "https://github.com/Dictionarry-Hub/database";
            syncStrategy = 60;
            autoPull = true;
            conflictStrategy = "override";
          }
          {
            name = "trash-pcd";
            repositoryUrl = "https://github.com/Dictionarry-Hub/trash-pcd";
            syncStrategy = 1440;
            autoPull = true;
            conflictStrategy = "override";
          }
        ];
      hasExpectedService =
        container.image == "ghcr.io/dictionarry-hub/profilarr:2.2.0"
        && container.ports == [ "0.0.0.0:6868:6868" ]
        && databasesService.wantedBy == [ "multi-user.target" ];
      hasExpectedSyncConfig =
        lib.hasInfix "INSERT INTO arr_sync_media_management" connectorsService.script
        && lib.hasInfix "INSERT INTO arr_sync_delay_profiles_config" connectorsService.script
        && lib.hasInfix "INSERT INTO arr_sync_quality_profiles" connectorsService.script
        && lib.hasInfix "SELECT id, 'on_pull'" connectorsService.script
        && lib.hasInfix "WHERE name = 'Dictionarry'" connectorsService.script
        && lib.hasInfix "2160p Remux" connectorsService.script;
      # The entrypoint chowns /config to PUID:PGID, so the data directory must
      # be owned by the profilarr user and the ids must come from it too.
      dataDir = generated.systemd.tmpfiles.settings."10-profilarr"."/var/lib/profilarr".d;
      containerUnit = generated.systemd.services.${container.serviceName};
      hasExpectedOwnership =
        dataDir.user == "profilarr"
        && dataDir.group == "profilarr"
        && generated.users.users.profilarr.isSystemUser
        && generated.users.users.profilarr.group == "profilarr"
        && generated.users.groups ? profilarr
        && !(container.environment ? PUID)
        && !(container.environment ? PGID)
        && lib.hasInfix "id -u profilarr" containerUnit.preStart
        && lib.hasInfix "id -g profilarr" containerUnit.preStart;
    in
    assertTest "profilarr-service-generation" (
      hasExpectedDatabases && hasExpectedService && hasExpectedSyncConfig && hasExpectedOwnership
    );

  # Test that nixflix.sonarr options generate correct systemd units
  sonarr-service-generation =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;
            sonarr = {
              enable = true;
              user = "testuser";
              config = {
                hostConfig = {
                  port = 8989;
                  username = "admin";
                  password._secret = "/run/secrets/sonarr-pass";
                };
                apiKey._secret = "/run/secrets/sonarr-api";
                releaseProfiles = [
                  {
                    name = "Extended/Superfan";
                    required = [ "extended" ];
                    tags = [ "extended" ];
                  }
                ];
                rootFolders = [ { path = "/media/tv"; } ];
              };
            };
          };
        }
      ];
      systemdUnits = config.config.systemd.services;
      releaseProfilesService = systemdUnits.sonarr-releaseprofiles;
      hasAllServices =
        systemdUnits ? sonarr
        && systemdUnits ? sonarr-config
        && systemdUnits ? sonarr-rootfolders
        && systemdUnits ? sonarr-releaseprofiles;
      hasReleaseProfile =
        lib.hasInfix "Extended/Superfan" releaseProfilesService.script
        && lib.hasInfix "releaseprofile" releaseProfilesService.script;
    in
    assertTest "sonarr-service-generation" (hasAllServices && hasReleaseProfile);

  # Test that nixflix.sonarr-anime options generate correct systemd units
  sonarr-anime-service-generation =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;
            sonarr-anime = {
              enable = true;
              user = "testuser";
              config = {
                hostConfig = {
                  port = 8990;
                  username = "admin";
                  password._secret = "/run/secrets/sonarr-pass";
                };
                apiKey._secret = "/run/secrets/sonarr-api";
                rootFolders = [ { path = "/media/anime"; } ];
              };
            };
          };
        }
      ];
      systemdUnits = config.config.systemd.services;
      hasAllServices =
        systemdUnits ? sonarr-anime
        && systemdUnits ? sonarr-anime-config
        && systemdUnits ? sonarr-anime-rootfolders;
    in
    assertTest "sonarr-anime-service-generation" hasAllServices;

  # Test that radarr options generate correct systemd units
  radarr-service-generation =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;
            radarr = {
              enable = true;
              user = "testuser";
              config = {
                hostConfig = {
                  port = 7878;
                  username = "admin";
                  password._secret = "/run/secrets/radarr-pass";
                };
                apiKey._secret = "/run/secrets/radarr-api";
                rootFolders = [ { path = "/media/movies"; } ];
              };
            };
          };
        }
      ];
      systemdUnits = config.config.systemd.services;
      hasAllServices =
        systemdUnits ? radarr && systemdUnits ? radarr-config && systemdUnits ? radarr-rootfolders;
    in
    assertTest "radarr-service-generation" hasAllServices;

  # Test that prowlarr with indexers generates correct systemd units
  prowlarr-service-generation =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;
            prowlarr = {
              enable = true;
              config = {
                hostConfig = {
                  port = 9696;
                  username = "admin";
                  password._secret = "/run/secrets/prowlarr-pass";
                };
                apiKey._secret = "/run/secrets/prowlarr-api";
                indexers = [
                  {
                    name = "1337x";
                    apiKey._secret = "/run/secrets/1337x-api";
                  }
                ];
              };
            };
          };
        }
      ];
      systemdUnits = config.config.systemd.services;
      hasAllServices =
        systemdUnits ? prowlarr && systemdUnits ? prowlarr-config && systemdUnits ? prowlarr-indexers;
    in
    assertTest "prowlarr-service-generation" hasAllServices;

  # Test that prowlarr with indexers generates correct systemd units
  sabnzbd-service-generation =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;
            usenetClients.sabnzbd = {
              enable = true;
              downloadsDir = "/downloads/usenet";
              settings = {
                misc = {
                  api_key._secret = pkgs.writeText "sabnzbd-apikey" "testapikey123456789abcdef";
                  nzb_key._secret = pkgs.writeText "sabnzbd-nzbkey" "testnzbkey123456789abcdef";
                  port = 8080;
                  host = "127.0.0.1";
                  url_base = "/sabnzbd";
                  ignore_samples = true;
                  direct_unpack = false;
                  article_tries = 5;
                };
                servers = [
                  {
                    name = "TestServer";
                    host = "news.example.com";
                    port = 563;
                    username._secret = pkgs.writeText "eweka-username" "testuser";
                    password._secret = pkgs.writeText "eweka-password" "testpass123";
                    connections = 10;
                    ssl = true;
                    priority = 0;
                  }
                ];
                categories = [
                  {
                    name = "tv";
                    dir = "tv";
                    priority = 0;
                    pp = 3;
                    script = "None";
                  }
                  {
                    name = "movies";
                    dir = "movies";
                    priority = 1;
                    pp = 2;
                    script = "None";
                  }
                ];
              };
            };
          };
        }
      ];
      systemdUnits = config.config.systemd.services;
      hasAllServices = systemdUnits ? sabnzbd;
    in
    assertTest "sabnzbd-service-generation" hasAllServices;

  # Test that seerr generates services with a remote Jellyfin (no local jellyfin)
  seerr-remote-jellyfin =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;
            seerr = {
              enable = true;
              apiKey._secret = "/run/secrets/seerr-api";
              jellyfin = {
                adminUsername = "remoteadmin";
                adminPassword = "remotepassword";
              };
            };
          };
        }
      ];
      systemdUnits = config.config.systemd.services;
    in
    assertTest "seerr-remote-jellyfin" (
      systemdUnits ? seerr
      && systemdUnits ? seerr-setup
      && systemdUnits ? seerr-jellyfin
      && systemdUnits ? seerr-libraries
      && systemdUnits ? seerr-user-settings
    );

  jellyfin-plugin-package-service-generation =
    let
      plugin = pkgs.runCommand "test-plugin-1.0.0" { } ''
        mkdir -p "$out"
        touch "$out/TestPlugin.dll"
      '';
      config = evalConfig [
        {
          nixflix = {
            enable = true;

            jellyfin = {
              enable = true;
              plugins."Test Plugin".package = plugin;
              users.admin = {
                password = "testpassword";
                policy.isAdministrator = true;
              };
            };
          };
        }
      ];
      systemdUnits = config.config.systemd.services;
      tmpfilesSettings = config.config.systemd.tmpfiles.settings;
      pluginPath = "${config.config.nixflix.jellyfin.dataDir}/plugins";
    in
    pkgs.runCommand "unit-test-jellyfin-plugin-package-service-generation" { } ''
      ${check "plugin service exists" (systemdUnits ? jellyfin-plugins)}
      ${check "plugin tmpfiles directory exists" (
        builtins.hasAttr pluginPath tmpfilesSettings."10-jellyfin"
      )}

      echo 'PASS: jellyfin-plugin-package-service-generation' > $out
    '';

  jellyfin-plugin-source-assertion =
    let
      result = builtins.tryEval (
        let
          config = evalConfig [
            {
              nixflix = {
                enable = true;

                jellyfin = {
                  enable = true;
                  plugins."Broken Plugin" = {
                    package = {
                      version = "1.0.0.0";
                    };
                    config.SomeSetting = true;
                  };
                  users.admin = {
                    password = "testpassword";
                    policy.isAdministrator = true;
                  };
                };
              };
            }
          ];
        in
        config.config.system.build.toplevel.drvPath
      );
    in
    assertTest "jellyfin-plugin-source-assertion" (!result.success);

  jellyfin-plugin-repo-service-generation =
    let
      targetAbi = "${pkgs.jellyfin.version}.0";
      manifest = pkgs.writeText "jellyfin-plugin-repo-service-generation.json" (
        builtins.toJSON [
          {
            guid = "c83d86bb-a1e0-4c35-a113-e2101cf4ee6b";
            name = "Intro Skipper";
            versions = [
              {
                version = "12.0.4.0";
                inherit targetAbi;
                sourceUrl = "https://github.com/intro-skipper/intro-skipper/releases/download/12.0/v12.0.4.0/intro-skipper-v12.0.4.0.zip";
              }
            ];
          }
        ]
      );
      config = evalConfig [
        {
          nixflix = {
            enable = true;

            jellyfin = {
              enable = true;
              system.pluginRepositories = lib.mkForce {
                "Test Repo" = {
                  url = builtins.unsafeDiscardStringContext "file://${manifest}";
                  hash = manifestHash manifest;
                  enabled = true;
                };
              };
              plugins."Intro Skipper" = {
                package = jellyfinPlugins.fromRepo {
                  version = "12.0.4.0";
                  hash = "sha256-sPEZXGB3s+YI1E9+qJ3EWdKFu2gdqK7LfNjV4QjMlnA=";
                };
              };
              users.admin = {
                password = "testpassword";
                policy.isAdministrator = true;
              };
            };
          };
        }
      ];
      pluginService = config.config.systemd.services.jellyfin-plugins;
    in
    pkgs.runCommand "unit-test-jellyfin-plugin-repo-service-generation" { } ''
      ${check "plugin service exists for repo-managed plugin" (
        config.config.systemd.services ? jellyfin-plugins
      )}
      ${check "repo-managed plugins resolve to package sync commands" (
        lib.hasInfix "Syncing packaged plugin: Intro Skipper" pluginService.script
      )}
      ${check "resolved plugin directory name appears in service script" (
        lib.hasInfix "Intro Skipper_12.0.4.0" pluginService.script
      )}

      echo 'PASS: jellyfin-plugin-repo-service-generation' > $out
    '';

  jellyfin-plugin-repo-ambiguity-assertion =
    let
      targetAbi = "${pkgs.jellyfin.version}.0";
      manifestA = pkgs.writeText "jellyfin-plugin-repo-a.json" (
        builtins.toJSON [
          {
            guid = "11111111-1111-1111-1111-111111111111";
            name = "Collision Plugin";
            versions = [
              {
                version = "1.0.0.0";
                inherit targetAbi;
                sourceUrl = "https://example.invalid/repo-a.zip";
              }
            ];
          }
        ]
      );
      manifestB = pkgs.writeText "jellyfin-plugin-repo-b.json" (
        builtins.toJSON [
          {
            guid = "22222222-2222-2222-2222-222222222222";
            name = "Collision Plugin";
            versions = [
              {
                version = "1.0.0.0";
                inherit targetAbi;
                sourceUrl = "https://example.invalid/repo-b.zip";
              }
            ];
          }
        ]
      );
      result = builtins.tryEval (
        let
          config = evalConfig [
            {
              nixflix = {
                enable = true;

                jellyfin = {
                  enable = true;
                  apiKey = "test-api-key";
                  system.pluginRepositories = lib.mkForce {
                    "Repo A" = {
                      url = builtins.unsafeDiscardStringContext "file://${manifestA}";
                      hash = manifestHash manifestA;
                      enabled = true;
                    };
                    "Repo B" = {
                      url = builtins.unsafeDiscardStringContext "file://${manifestB}";
                      hash = manifestHash manifestB;
                      enabled = true;
                    };
                  };
                  plugins."Collision Plugin" = {
                    package = jellyfinPlugins.fromRepo {
                      version = "1.0.0.0";
                      hash = lib.fakeHash;
                    };
                  };
                  users.admin = {
                    password = "testpassword";
                    policy.isAdministrator = true;
                  };
                };
              };
            }
          ];
        in
        config.config.system.build.toplevel.drvPath
      );
    in
    assertTest "jellyfin-plugin-repo-ambiguity-assertion" (!result.success);

  jellyfin-integration =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;

            jellyfin = {
              enable = true;
              users.admin = {
                password = "testpassword";
                policy.isAdministrator = true;
              };
            };

            radarr = {
              enable = true;
              mediaDirs = [ "/media/movies" ];
              config = {
                hostConfig = {
                  port = 7878;
                  username = "admin";
                  password._secret = "/run/secrets/radarr-pass";
                };
                apiKey._secret = "/run/secrets/radarr-api";
                rootFolders = [ { path = "/media/movies"; } ];
              };
            };

            sonarr = {
              enable = true;
              mediaDirs = [ "/media/shows" ];
              config = {
                hostConfig = {
                  port = 8989;
                  username = "admin";
                  password._secret = "/run/secrets/sonarr-pass";
                };
                apiKey._secret = "/run/secrets/sonarr-api";
                rootFolders = [ { path = "/media/shows"; } ];
              };
            };

            sonarr-anime = {
              enable = true;
              mediaDirs = [ "/media/anime" ];
              config = {
                hostConfig = {
                  port = 8990;
                  username = "admin";
                  password._secret = "/run/secrets/sonarr-anime-pass";
                };
                apiKey._secret = "/run/secrets/sonarr-anime-api";
                rootFolders = [ { path = "/media/anime"; } ];
              };
            };

            lidarr = {
              enable = true;
              mediaDirs = [ "/media/music" ];
              config = {
                hostConfig = {
                  port = 8686;
                  username = "admin";
                  password._secret = "/run/secrets/lidarr-pass";
                };
                apiKey._secret = "/run/secrets/lidarr-api";
                rootFolders = [ { path = "/media/music"; } ];
              };
            };
          };
        }
      ];

      inherit (config.config.nixflix.jellyfin) libraries;
    in
    pkgs.runCommand "unit-test-jellyfin-integration" { } ''
      ${check "Movies library exists" (libraries ? Movies)}
      ${check "Movies library has correct collectionType" (libraries.Movies.collectionType == "movies")}
      ${check "Movies library has correct path" (builtins.elem "/media/movies" libraries.Movies.paths)}

      ${check "Shows library exists" (libraries ? Shows)}
      ${check "Shows library has correct collectionType" (libraries.Shows.collectionType == "tvshows")}
      ${check "Shows library has correct path" (builtins.elem "/media/shows" libraries.Shows.paths)}

      ${check "Anime library exists" (libraries ? Anime)}
      ${check "Anime library has correct collectionType" (libraries.Anime.collectionType == "tvshows")}
      ${check "Anime library has correct path" (builtins.elem "/media/anime" libraries.Anime.paths)}

      ${check "Music library exists" (libraries ? Music)}
      ${check "Music library has correct collectionType" (libraries.Music.collectionType == "music")}
      ${check "Music library has correct path" (builtins.elem "/media/music" libraries.Music.paths)}

      echo 'PASS: jellyfin-integration' > $out
    '';

  download-clients-no-reverse-proxy =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;
            nginx.enable = false;

            radarr = {
              enable = true;
              config = {
                hostConfig = {
                  port = 7878;
                  username = "admin";
                  password._secret = "/run/secrets/radarr-pass";
                };
                apiKey._secret = "/run/secrets/radarr-api";
                rootFolders = [ { path = "/media/movies"; } ];
              };
            };

            usenetClients.sabnzbd = {
              enable = true;
              settings.misc = {
                api_key._secret = pkgs.writeText "sabnzbd-apikey" "testapikey123456789abcdef";
                nzb_key._secret = pkgs.writeText "sabnzbd-nzbkey" "testnzbkey123456789abcdef";
                port = 8080;
                url_base = "/sabnzbd";
              };
            };
          };
        }
      ];
      radarrCfg = config.config.nixflix.radarr;
      sabnzbdCfg = config.config.nixflix.usenetClients.sabnzbd;
      downloadClientsService = config.config.systemd.services."radarr-downloadclients";
    in
    pkgs.runCommand "unit-test-download-clients-no-reverse-proxy" { } ''
      ${check "radarr bindAddress is 0.0.0.0 when nginx is disabled" (
        radarrCfg.config.hostConfig.bindAddress == "0.0.0.0"
      )}
      ${check "radarr connectionAddress is 127.0.0.1 (not 0.0.0.0)" (
        radarrCfg.connectionAddress == "127.0.0.1"
      )}
      ${check "sabnzbd connectionAddress is 127.0.0.1 (not 0.0.0.0)" (
        sabnzbdCfg.connectionAddress == "127.0.0.1"
      )}
      ${check "radarr-downloadclients ExecStartPre uses connectionAddress" (
        lib.hasInfix "127.0.0.1" downloadClientsService.serviceConfig.ExecStartPre
      )}
      ${check "radarr-downloadclients ExecStartPre does not use 0.0.0.0" (
        !lib.hasInfix "0.0.0.0" downloadClientsService.serviceConfig.ExecStartPre
      )}
      ${check "radarr-downloadclients script uses connectionAddress" (
        lib.hasInfix "127.0.0.1" downloadClientsService.script
      )}
      ${check "radarr-downloadclients script does not use 0.0.0.0" (
        !lib.hasInfix "0.0.0.0" downloadClientsService.script
      )}
      echo 'PASS: download-clients-no-reverse-proxy' > $out
    '';

  notif-service-scoping =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;

            jellyfin = {
              enable = true;
              apiKey = "test-jellyfin-key";
            };

            navidrome.enable = true;
            navidrome.users.admin = {
              userName = "admin";
              isAdmin = true;
              password = "testpassword";
            };

            sonarr = {
              enable = true;
              config = {
                hostConfig.port = 8989;
                apiKey._secret = "/run/secrets/sonarr-api";
              };
            };

            radarr = {
              enable = true;
              config = {
                hostConfig.port = 7878;
                apiKey._secret = "/run/secrets/radarr-api";
              };
            };

            lidarr = {
              enable = true;
              config = {
                hostConfig.port = 8686;
                apiKey._secret = "/run/secrets/lidarr-api";
              };
            };
          };
        }
      ];
      sonarrNotifications = config.config.systemd.services."sonarr-notifications";
      radarrNotifications = config.config.systemd.services."radarr-notifications";
      lidarrNotifications = config.config.systemd.services."lidarr-notifications";
    in
    pkgs.runCommand "unit-test-notif-service-scoping" { } ''
      ${check "sonarr-notifications ExecStartPre uses connectionAddress" (
        lib.hasInfix "127.0.0.1" sonarrNotifications.serviceConfig.ExecStartPre
      )}
      ${check "sonarr-notifications ExecStartPre does not use 0.0.0.0" (
        !lib.hasInfix "0.0.0.0" sonarrNotifications.serviceConfig.ExecStartPre
      )}
      ${check "radarr-notifications script uses connectionAddress" (
        lib.hasInfix "127.0.0.1" radarrNotifications.script
      )}
      ${check "sonarr-notifications is configured for Emby/Jellyfin" (
        lib.hasInfix "Emby / Jellyfin" sonarrNotifications.script
      )}
      ${check "sonarr-notifications is NOT configured for Subsonic (Lidarr-only)" (
        !lib.hasInfix "Subsonic" sonarrNotifications.script
      )}
      ${check "lidarr-notifications is configured for Emby/Jellyfin" (
        lib.hasInfix "Emby / Jellyfin" lidarrNotifications.script
      )}
      ${check "lidarr-notifications is configured for Subsonic" (
        lib.hasInfix "Subsonic" lidarrNotifications.script
      )}
      echo 'PASS: notif-service-scoping' > $out
    '';

  jellyfin-subtitles =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;

            jellyfin = {
              enable = true;
              apiKey = "test-api-key";

              users.admin = {
                password = "testpassword";
                policy.isAdministrator = true;
              };

              plugins = {
                "Open Subtitles" = {
                  enable = true;
                  config = {
                    Username = "testsubsuser";
                    Password = "opensubs_test_password";
                  };
                };

                subbuzz = {
                  enable = true;
                  config = {
                    EnableOpenSubtitles = true;
                    EnableYifySubtitles = true;
                    MinScore = 60;
                    Cache.SubLifeInMinutes = "Always";
                    SubPostProcessing.EncodeSubtitlesToUTF8 = false;
                  };
                };

                "Subtitle Extract" = {
                  enable = true;
                  config = {
                    ExtractionDuringLibraryScan = true;
                    IncludeTextSubtitles = true;
                    IncludeGraphicalSubtitles = false;
                  };
                };
              };

              libraries."Subtitle Movies" = {
                collectionType = "movies";
                paths = [ "/media/movies" ];
                subtitleFetcherOrder = [
                  "Open Subtitles"
                  "subbuzz"
                ];
                subtitleDownloadLanguages = [
                  "eng"
                  "spa"
                ];
                saveSubtitlesWithMedia = true;
                allowEmbeddedSubtitles = "AllowAll";
                requirePerfectSubtitleMatch = true;
                skipSubtitlesIfAudioTrackMatches = false;
                skipSubtitlesIfEmbeddedSubtitlesPresent = true;
              };
            };
          };
        }
      ];
      pluginService = config.config.systemd.services.jellyfin-plugins;
      jellyfinCfg = config.config.nixflix.jellyfin;
    in
    pkgs.runCommand "unit-test-jellyfin-subtitles" { } ''
      ${check "jellyfin-plugins service exists" (config.config.systemd.services ? jellyfin-plugins)}

      ${check "Open Subtitles plugin sync command in service script" (
        lib.hasInfix "Syncing packaged plugin: Open Subtitles" pluginService.script
      )}
      ${check "subbuzz plugin sync command in service script" (
        lib.hasInfix "Syncing packaged plugin: subbuzz" pluginService.script
      )}
      ${check "Subtitle Extract plugin sync command in service script" (
        lib.hasInfix "Syncing packaged plugin: Subtitle Extract" pluginService.script
      )}

      ${check "Open Subtitles plugin directory name in service script" (
        lib.hasInfix "Open Subtitles_25.0.0.0" pluginService.script
      )}
      ${check "subbuzz plugin directory name in service script" (
        lib.hasInfix "subbuzz_1.5.0.0" pluginService.script
      )}
      ${check "Subtitle Extract plugin directory name in service script" (
        lib.hasInfix "Subtitle Extract_8.0.0.0" pluginService.script
      )}

      ${check "subbuzz EnableOpenSubtitles config value" jellyfinCfg.plugins.subbuzz.config.EnableOpenSubtitles}
      ${check "subbuzz EnableYifySubtitles config value" jellyfinCfg.plugins.subbuzz.config.EnableYifySubtitles}
      ${check "subbuzz MinScore config value" (jellyfinCfg.plugins.subbuzz.config.MinScore == 60)}
      ${check "subbuzz Cache.SubLifeInMinutes is 1000001 when set to Always" (
        jellyfinCfg.plugins.subbuzz.config.Cache.SubLifeInMinutes == 1000001
      )}
      ${check "subbuzz SubPostProcessing.EncodeSubtitlesToUTF8 config value" (
        !jellyfinCfg.plugins.subbuzz.config.SubPostProcessing.EncodeSubtitlesToUTF8
      )}

      ${check "Open Subtitles Username config value" (
        jellyfinCfg.plugins."Open Subtitles".config.Username == "testsubsuser"
      )}

      ${check "Subtitle Extract ExtractionDuringLibraryScan config value"
        jellyfinCfg.plugins."Subtitle Extract".config.ExtractionDuringLibraryScan
      }
      ${check "Subtitle Extract IncludeTextSubtitles config value"
        jellyfinCfg.plugins."Subtitle Extract".config.IncludeTextSubtitles
      }
      ${check "Subtitle Extract IncludeGraphicalSubtitles config value" (
        !jellyfinCfg.plugins."Subtitle Extract".config.IncludeGraphicalSubtitles
      )}

      ${check "Subtitle Movies library exists" (jellyfinCfg.libraries ? "Subtitle Movies")}
      ${check "Library subtitle fetcher order" (
        jellyfinCfg.libraries."Subtitle Movies".subtitleFetcherOrder == [
          "Open Subtitles"
          "subbuzz"
        ]
      )}
      ${check "Library subtitle download languages" (
        builtins.elem "eng" jellyfinCfg.libraries."Subtitle Movies".subtitleDownloadLanguages
        && builtins.elem "spa" jellyfinCfg.libraries."Subtitle Movies".subtitleDownloadLanguages
      )}
      ${check "Library saveSubtitlesWithMedia"
        jellyfinCfg.libraries."Subtitle Movies".saveSubtitlesWithMedia
      }
      ${check "Library allowEmbeddedSubtitles" (
        jellyfinCfg.libraries."Subtitle Movies".allowEmbeddedSubtitles == "AllowAll"
      )}
      ${check "Library requirePerfectSubtitleMatch"
        jellyfinCfg.libraries."Subtitle Movies".requirePerfectSubtitleMatch
      }
      ${check "Library skipSubtitlesIfEmbeddedSubtitlesPresent"
        jellyfinCfg.libraries."Subtitle Movies".skipSubtitlesIfEmbeddedSubtitlesPresent
      }
      ${check "Library skipSubtitlesIfAudioTrackMatches" (
        !jellyfinCfg.libraries."Subtitle Movies".skipSubtitlesIfAudioTrackMatches
      )}

      echo 'PASS: jellyfin-subtitles' > $out
    '';

  # LoadCredential replaces the old -env root service; verify the generated unit
  arr-load-credential =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;
            sonarr = {
              enable = true;
              config = {
                apiKey._secret = "/run/secrets/sonarr-api";
                hostConfig = {
                  port = 8989;
                  username = "admin";
                  password._secret = "/run/secrets/sonarr-pass";
                };
              };
            };
          };
        }
      ];
      services = config.config.systemd.services;
      svc = services.sonarr.serviceConfig;
    in
    pkgs.runCommand "unit-test-arr-load-credential" { } ''
      ${check "sonarr-env service no longer exists" (!services ? sonarr-env)}
      ${check "LoadCredential set for secret-ref apiKey" (
        builtins.elem "apiKey:/run/secrets/sonarr-api" svc.LoadCredential
      )}
      ${check "no EnvironmentFile" (!svc ? EnvironmentFile)}
      echo 'PASS: arr-load-credential' > $out
    '';

  # hostConfig assertion: username and password must both be set or both be null
  hostconfig-username-requires-password =
    let
      result = builtins.tryEval (
        let
          config = evalConfig [
            {
              nixflix = {
                enable = true;
                sonarr = {
                  enable = true;
                  config.hostConfig = {
                    port = 8989;
                    username = "admin";
                    # password left at default null
                  };
                };
              };
            }
          ];
        in
        config.config.system.build.toplevel.drvPath
      );
    in
    assertTest "hostconfig-username-requires-password" (!result.success);

  hostconfig-password-requires-username =
    let
      result = builtins.tryEval (
        let
          config = evalConfig [
            {
              nixflix = {
                enable = true;
                sonarr = {
                  enable = true;
                  config.hostConfig = {
                    port = 8989;
                    username = null;
                    password._secret = "/run/secrets/sonarr-pass";
                  };
                };
              };
            }
          ];
        in
        config.config.system.build.toplevel.drvPath
      );
    in
    assertTest "hostconfig-password-requires-username" (!result.success);

  # lidarr metadata/quality profile assertions: at least one profile must be present
  lidarr-qualityprofiles-empty-assertion =
    let
      result = builtins.tryEval (
        let
          config = evalConfig [
            {
              nixflix = {
                enable = true;
                lidarr = {
                  enable = true;
                  config.qualityProfiles = [ ];
                };
              };
            }
          ];
        in
        config.config.system.build.toplevel.drvPath
      );
    in
    assertTest "lidarr-qualityprofiles-empty-assertion" (!result.success);

  lidarr-metadataprofiles-empty-assertion =
    let
      result = builtins.tryEval (
        let
          config = evalConfig [
            {
              nixflix = {
                enable = true;
                lidarr = {
                  enable = true;
                  config.metadataProfiles = [ ];
                };
              };
            }
          ];
        in
        config.config.system.build.toplevel.drvPath
      );
    in
    assertTest "lidarr-metadataprofiles-empty-assertion" (!result.success);

  # https://github.com/kiriwalawren/nixflix/issues/270
  # settings.auth/settings.server must mirror config.hostConfig so that the
  # environment variables actually reflect what the user configured there.
  hostconfig-drives-settings-auth =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;
            radarr = {
              enable = true;
              config = {
                hostConfig = {
                  port = 7878;
                  urlBase = "/radarr";
                  authenticationMethod = "external";
                  authenticationRequired = "disabledForLocalAddresses";
                  username = "admin";
                  password._secret = "/run/secrets/radarr-pass";
                };
                apiKey._secret = "/run/secrets/radarr-api";
                rootFolders = [ { path = "/media/movies"; } ];
              };
            };
          };
        }
      ];
      radarrCfg = config.config.nixflix.radarr;
      environment = config.config.systemd.services.radarr.environment;
    in
    pkgs.runCommand "unit-test-hostconfig-drives-settings-auth" { } ''
      ${check "settings.auth.method mirrors hostConfig.authenticationMethod" (
        radarrCfg.settings.auth.method == "External"
      )}
      ${check "settings.auth.required mirrors hostConfig.authenticationRequired" (
        radarrCfg.settings.auth.required == "DisabledForLocalAddresses"
      )}
      ${check "settings.server.port mirrors hostConfig.port" (radarrCfg.settings.server.port == 7878)}
      ${check "settings.server.urlBase mirrors hostConfig.urlBase" (
        radarrCfg.settings.server.urlBase == "/radarr"
      )}
      ${check "RADARR__AUTH__METHOD env var reflects hostConfig" (
        environment.RADARR__AUTH__METHOD == "External"
      )}
      ${check "RADARR__AUTH__REQUIRED env var reflects hostConfig" (
        environment.RADARR__AUTH__REQUIRED == "DisabledForLocalAddresses"
      )}
      echo 'PASS: hostconfig-drives-settings-auth' > $out
    '';

  # A user should be able to override settings.auth directly (normal priority,
  # no lib.mkForce needed) since the hostConfig-derived value is only mkDefault.
  settings-auth-overrides-hostconfig =
    let
      config = evalConfig [
        {
          nixflix = {
            enable = true;
            radarr = {
              enable = true;
              config = {
                hostConfig = {
                  port = 7878;
                  authenticationMethod = "forms";
                  username = "admin";
                  password._secret = "/run/secrets/radarr-pass";
                };
                apiKey._secret = "/run/secrets/radarr-api";
                rootFolders = [ { path = "/media/movies"; } ];
              };
              settings.auth.method = "External";
            };
          };
        }
      ];
      radarrCfg = config.config.nixflix.radarr;
    in
    assertTest "settings-auth-overrides-hostconfig" (radarrCfg.settings.auth.method == "External");

  nested-secret-in-list-jq-filter =
    let
      rawConfig = {
        Entries = [
          {
            Name = "first";
            Value._secret = secretFile;
          }
        ];
      };
      secretFile = pkgs.writeText "entry-value" "s3cr3t-value\n";
      plainFile = pkgs.writeText "plain.json" (builtins.toJSON (secrets.stripSecretRefs rawConfig));
      jqSecrets = secrets.mkNestedJqSecretArgs rawConfig;
    in
    pkgs.runCommand "unit-test-nested-secret-in-list-jq-filter"
      {
        nativeBuildInputs = [ pkgs.jq ];
      }
      ''
        merged=$(
          echo '{"Entries":[]}' \
            | jq \
                ${jqSecrets.flagsString} \
                --argjson plain "$(cat ${plainFile})" \
                '. * $plain | ${lib.concatStringsSep " | " jqSecrets.assignments}'
        )

        value=$(echo "$merged" | jq -r '.Entries[0].Value')
        if [ "$value" != "s3cr3t-value" ]; then
          echo "FAIL: expected substituted value, got '$value'" && exit 1
        fi
        echo 'PASS: nested-secret-in-list-jq-filter' > $out
      '';
}
